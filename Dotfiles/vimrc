" neobundle {{{
if has('vim_starting')
	set runtimepath+=~/.vim/bundle/neobundle.vim/
endif
call neobundle#rc(expand('~/.vim/bundle/'))
NeoBundleFetch 'Shougo/neobundle.vim'

" nicer editing bundles
NeoBundle 'Raimondi/delimitMate'
NeoBundle 'ervandew/supertab'
NeoBundle 'tomtom/tcomment_vim'
NeoBundle 'tpope/vim-surround'

" probably unnecessary, but nice to have
NeoBundle 'bling/vim-airline'
NeoBundle 'jpalardy/vim-slime'

" load this only on OSX
if system("uname -s") == "Darwin\n"
	NeoBundle 'airblade/vim-gitgutter'
	NeoBundle 'scrooloose/syntastic'
endif

" shugo bundles
NeoBundle 'Shougo/unite.vim'
NeoBundle 'Shougo/vimfiler.vim'
" NeoBundle 'Shougo/vimshell.vim'
NeoBundle 'Shougo/vimproc', {
	\ 'build': {
	\		'mac': 'make -f make_mac.mak',
	\		'unix': 'make -f make_unix.mak',
	\		},
	\ }

" custom syntax bundles
NeoBundle 'briancollins/vim-jst'
NeoBundle 'davidoc/taskpaper.vim'
NeoBundle 'jelera/vim-javascript-syntax'
NeoBundle 'kchmck/vim-coffee-script'
NeoBundle 'moll/vim-node'
NeoBundle 'pangloss/vim-javascript'
NeoBundle 'plasticboy/vim-markdown'
NeoBundle 'sophacles/vim-processing'
NeoBundle 'sudar/vim-arduino-syntax'
NeoBundle 'tikhomirov/vim-glsl'

" }}}
" vim settings {{{

" make vim more useful
set nocompatible

" automaticaly cd into dir - breaks vimshell
" set autochdir
" autocmd BufEnter * silent! lcd %:p:h

" yank and paste with system clipboard
set clipboard=unnamed

" watch for file changes
set autoread

" redraw only when needed and fast tty
set lazyredraw
set ttyfast
set ttyscroll=3

" don't beep
set noeb vb t_vb=

" disable startup message
set shortmess+=I

" syntax coloring
syntax enable

" don't try to highlight longer lines
set synmaxcol=256

" encoding
set encoding=utf-8
set fileencodings=utf-8

" indentation
filetype plugin indent on
set autoindent
set tabstop=2
set shiftwidth=2
set softtabstop=2

" html indent fixes
let g:html_indent_tags=['p', 'li', 'ol', 'ul', 'div', 'head', 'html', 'body']

" additional settings
set backspace=indent,eol,start
set number
set linebreak
set ruler
set scrolloff=3

" hidden chars liketextmate, disabled by default
set list
" set listchars=tab:â–¸\ ,trail:âˆ™,eol:Â¬
set listchars=tab:â–¸\ ,trail:âˆ™

" buffers/tabs settings
set switchbuf=useopen,usetab,newtab
set hidden

" no backups or swapfiles, with centralized and persistent undo history
set nobackup
set noswapfile
set undofile
set undodir=~/.vim/undo//

" fix for node fs.watch
set backupcopy=yes

" long history and undo
set history=1000
set undolevels=1000
set undoreload=10000

" search settings
set hlsearch
set ignorecase
set smartcase
set incsearch

" always disaplay status line
set laststatus=2

" show tabline only when needed
set showtabline=1

" don't show current mode/cmd
set noshowmode
set noshowcmd

" completion in command line
set wildmenu

" insert completion height
set pumheight=10

" code folding
set foldmethod=syntax
set foldlevel=999
set foldcolumn=0

" color theme
set t_Co=256
set background=dark
" colorscheme tomorrow-night

let g:solarized_bold=0
let g:solarized_italic=0
let g:solarized_underline=0
colorscheme solarized

" open splits to the right and bottom
set splitright
set splitbelow

" set split char to space (looks nice)
set fillchars+=vert:\ 

" extended % matching
runtime macros/matchit.vim

" }}}
" autocommands {{{

" return to same line after reopenning file
augroup line_return
	au!
	au BufReadPost *
		\ if line("'\"") > 0 && line("'\"") <= line("$") |
		\		execute 'normal! g`"zvzz' |
		\ endif
augroup END

" automatically save and restore view
" augroup save_view
" 	au!
"
" 	autocmd BufWrite * mkview
" 	autocmd BufNewFile,BufRead * silent loadview
" augroup END

" automatically resize splits when resizing window
augroup split_resize
	au!
	au VimResized * wincmd =
augroup END

" set proper syntaxes
augroup syntaxes
	au!

	au BufNewFile,BufRead *.mm set filetype=objc
	au BufNewFile,BufRead *.tpl let b:jst_subtype='html'
	au BufNewFile,BufRead *.tpl set filetype=jst

	" au BufNewFile,BufRead ~/Dropbox/Notes/* setlocal filetype=markdown

	au FileType int-node setlocal filetype=javascript
	au FileType int-lein setlocal filetype=clojure
augroup END

" custom settings based on filetype
augroup ft_settings
	au!

  " au FileType javascript call JavaScriptFold()
	au FileType coffee setlocal shiftwidth=2 expandtab
	au FileType vim setlocal foldmethod=marker foldlevel=0
augroup END

" no numbers based on file types
augroup nonumbers
	au!

	au FileType vimfiler  setlocal nonumber
	au FileType vimshell  setlocal nonumber
	au FileType int-node  setlocal nonumber
	au FileType int-lein  setlocal nonumber
	au FileType text      setlocal nonumber
	au FileType markdown  setlocal nonumber
	au FileType taskpaper setlocal nonumber
augroup END

" don't show special chars in 'normal' files
augroup nolist
	au!

	au FileType text      setlocal nolist
	au FileType markdown  setlocal nolist
	au FileType taskpaper setlocal nolist
augroup END

" cursorlines in special buffers
augroup cursorlines
	au!
	au FileType vimfiler setlocal cursorline
augroup END

" }}}
" window titles {{{

" set path
if system('uname')=="Darwin\n"
	let $PATH="/usr/local/bin:" . $PATH . ":/Users/Szymon/Dropbox/Code/Scripts"
endif

" set nice window title
function! DirPath()
	let l:splited = split(getcwd(), "/")
	let l:cut = 3
	if len(l:splited) < l:cut
		let cut = len(l:splited)
	endif

	let l:cut = -l:cut

	return join(l:splited[l:cut : -1], "/")
endfunction

function! WindowTitle()
	return "vim: " . DirPath() . "/" . fnamemodify(expand("%:f"), ":.")
endfunction

" set title on start as simple 'vim'
set title
set titlestring=vim

" update titlestring with proper title
augroup title_titlestring
	au!
	au BufEnter * let &titlestring = WindowTitle()
augroup END

" only needed in screen
if &term == "screen-256color"
	" screen caption is set by iconstring
	set t_IS=k
	set t_IE=\
	set icon

	augroup title_iconstring
		au!
		au BufEnter * let &iconstring = WindowTitle()
	augroup END

	" screen window title is set by titlestring
	set t_ts=]2;
	set t_fs=\
endif

" }}}
" plugin: airline {{{

" airline settings
let g:airline_left_sep=''
let g:airline_right_sep=''
let g:airline_inactive_collapse=0
let g:airline#extensions#whitespace#enabled=0
let g:airline#extensions#syntastic#enabled=0
let g:airline#extensions#hunks#enabled=0
let g:airline_section_b="%{DirPath()}"
let g:airline_section_c="%t"
let g:airline_section_x=""
let g:airline_section_y=""

" show tabline themed with airline only when needed
let g:airline#extensions#tabline#enabled=1
let g:airline#extensions#tabline#show_buffers=0
let g:airline#extensions#tabline#show_tab_type=0
let g:airline#extensions#tabline#tab_min_count=2
let g:airline#extensions#tabline#show_tab_nr=0
let g:airline#extensions#tabline#left_sep=''
let g:airline#extensions#tabline#left_alt_sep=''
let g:airline#extensions#tabline#right_sep=''
let g:airline#extensions#tabline#right_alt_sep=''

" remove bold fonts from airline tomorrow theme
let g:airline_theme_patch_func = 'AirlineThemePatch'
function! AirlineThemePatch(palette)
	if g:airline_theme == 'tomorrow' || g:airline_theme == "solarized"
		let palettes = [a:palette.normal, a:palette.insert, a:palette.replace, a:palette.visual, a:palette.accents]

		for palette in palettes
			for colors in values(palette)
				if len(colors) >= 5
					let colors[4] = 'none'
				endif
			endfor
			" let palette.airline_c[3] = '0'
		endfor

		if g:airline_theme == 'tomorrow' && !has("gui_running")
			for colors in values(a:palette.inactive)
				let colors[2] = '245'
				let colors[3] = '0'
			endfor
		endif

		" if g:airline_theme == 'solarized' && !has("gui_running")
		if g:airline_theme == 'solarized'
			for colors in values(a:palette.inactive)
				if &background == "dark"
					let colors[0] = '#586e75'
					let colors[1] = '#073642'
				else
					let colors[0] = '#93a1a1'
					let colors[1] = '#eee8d5'
				endif
				let colors[2] = '10'
				let colors[3] = '0'
			endfor
		endif
	endif
endfunction

" }}}
" plugin: syntastic {{{

let g:syntastic_enable_highlighting=0
let g:syntastic_check_on_open=1
let g:syntastic_check_on_wq=0
let g:syntastic_always_populate_loc_list=1
let g:syntastic_error_symbol='!'
let g:syntastic_style_error_symbol='!'
let g:syntastic_warning_symbol='?'
let g:syntastic_style_warning_symbol='?'

" }}}
" plugin: supertab {{{

let g:SuperTabDefaultCompletionType="context"
let g:SuperTabContextDefaultCompletionType="<c-n>"

" }}}
" plugin: unite {{{

" settings
let g:unite_data_directory=expand('~/.vim/unite')
let g:unite_source_history_yank_enable=1
let g:unite_cursor_line_time=0
let g:unite_cursor_line_highlight='CursorLine'
call unite#filters#matcher_default#use(['matcher_fuzzy'])

" mappings
nnoremap <silent> <C-p> :Unite -buffer-name=files -start-insert file_rec/async:!<cr>
nnoremap <silent> <C-b> :Unite -buffer-name=buffer -start-insert buffer<cr>
nnoremap <silent> <C-y> :Unite -buffer-name=yank -start-insert history/yank<cr>

" grep
if executable('ag')
	let g:unite_source_grep_command='ag'
	let g:unite_source_grep_default_opts=
	  \ '--line-numbers --nocolor --nogroup --hidden --ignore ' .
	  \  '''.hg'' --ignore ''.svn'' --ignore ''.git'' --ignore ''.bzr'''
	let g:unite_source_grep_recursive_opt=''
	nnoremap <silent> <C-g> :Unite -buffer-name=grep -no-quit -keep-focus -no-start-insert grep:.<cr>
endif

" NV notes
if isdirectory($HOME . "/Dropbox/Notes")
	command! -nargs=* Notes :silent Unite -buffer-name=notes -start-insert file_rec/async:~/Dropbox/Notes -input=<args>
endif

" TaskPaper tasks
if isdirectory($HOME . "/Dropbox/Tasks")
	command! -nargs=* Tasks :silent Unite -buffer-name=tasks -start-insert file_rec/async:~/Dropbox/Tasks -input=<args>
endif

" }}}
" plugin: vimfiler {{{

let g:vimfiler_enable_auto_cd=1
let g:vimfiler_as_default_explorer=1
let g:vimfiler_directory_display_top=1
let g:vimfiler_tree_leaf_icon=' '
let g:vimfiler_tree_opened_icon='â–¾'
let g:vimfiler_tree_closed_icon='â–¸'
let g:vimfiler_file_icon=' '
let g:vimfiler_marked_file_icon='*'
let g:vimfiler_data_directory=expand('~/.vim/vimfiler')

" open vimfilerexplorer
nnoremap <silent> <C-n> :VimFilerExplorer<cr>

" }}}
" plugin: vimshell {{{

let g:vimshell_prompt_expr='escape(DirPath(),  "\\[]()?! "). " â†’ "'
let g:vimshell_prompt_pattern='^\(\f\|\\.\)\+ â†’ '

" mappings
nmap <silent> <leader>vs :VimShell<cr>
nmap <silent> <leader>ss :VimShellSendString<cr>
vmap <silent> <leader>ss :VimShellSendString<cr>

" run interactive vimshells
command! ShellNode VimShellInteractive node
command! ShellClojure :VimShellInteractive lein repl
command! ShellCljsListen :VimShellInteractive lein trampoline cljsbuild repl-listen

" }}}
" plugin: vimproc {{{

" simple app running functions
command! Plask :VimProcBang plask %
command! Node :VimProcBang node %

" }}}
" plugin: vim-slime {{{

nmap <silent> <leader>sl :SlimeSend<cr>

" }}}
" custom commands {{{

" kills trailing whitespaces
command! KillWhitespace :normal :%s/\s\+$//g<cr>

" shows syntax highlight group for element
command! ShowSyntax :echo "hi<" . synIDattr(synID(line("."),col("."),1),"name") . '> trans<'
\ . synIDattr(synID(line("."),col("."),0),"name") . "> lo<"
\ . synIDattr(synIDtrans(synID(line("."),col("."),1)),"name") . ">"<CR>

" destroy all buffers that are not open in any tabs or windows
" https://github.com/artnez/vim-wipeout/blob/master/plugin/wipeout.vim
command! -bang Wipeout :call Wipeout(<bang>0)
function! Wipeout(bang)
	" figure out which buffers are visible in any tab
	let visible = {}
	for t in range(1, tabpagenr('$'))
		for b in tabpagebuflist(t)
			let visible[b] = 1
		endfor
	endfor
	" close any buffer that are loaded and not visible
	let l:tally = 0
	let l:cmd = 'bw'
	if a:bang
		let l:cmd .= '!'
	endif
	for b in range(1, bufnr('$'))
		if buflisted(b) && !has_key(visible, b)
			let l:tally += 1
			exe l:cmd . ' ' . b
		endif
	endfor
	echon "Deleted " . l:tally . " buffer" . (l:tally == 1 ? "" : "s")
endfun

" open URL in browser
if system("uname -s") == "Darwin\n"
	function! s:OpenURL(url)
		echo a:url
		exe "!open \"" . a:url . "\""
		redraw!
	endfunction

	command! -nargs=1 OpenURL :call s:OpenURL(<q-args>)

	nnoremap gb :OpenURL <cfile><cr>
endif

" toggle background
function! ToggleBackground()
	if &background == "dark"
		set background=light
	else
		set background=dark
	endif
endfunction
command! ToggleBackground :call ToggleBackground()

" visual selection function
function! VisualSelection(direction) range
	let l:saved_reg = @"
	execute "normal! vgvy"

	let l:pattern = escape(@", '\\/.*$^~[]')
	let l:pattern = substitute(l:pattern, "\n$", "", "")

	if a:direction == 'b'
		execute "normal ?" . l:pattern . "^M"
	elseif a:direction == 'gv'
		call CmdLine("vimgrep " . '/'. l:pattern . '/' . ' **/*.')
	elseif a:direction == 'replace'
		call CmdLine("%s" . '/'. l:pattern . '/')
	elseif a:direction == 'f'
		execute "normal /" . l:pattern . "^M"
	endif

	let @/ = l:pattern
	let @" = l:saved_reg
endfunction

" visual mode pressing * or # searches for the current selection
vnoremap <silent> * :call VisualSelection('f')<cr>
vnoremap <silent> # :call VisualSelection('b')<cr>

" motion for 'next' and 'last' text object
" from: https://gist.github.com/sjl/3762227
onoremap an :<c-u>call <SID>NextTextObject('a', 'f')<cr>
xnoremap an :<c-u>call <SID>NextTextObject('a', 'f')<cr>
onoremap in :<c-u>call <SID>NextTextObject('i', 'f')<cr>
xnoremap in :<c-u>call <SID>NextTextObject('i', 'f')<cr>

onoremap al :<c-u>call <SID>NextTextObject('a', 'F')<cr>
xnoremap al :<c-u>call <SID>NextTextObject('a', 'F')<cr>
onoremap il :<c-u>call <SID>NextTextObject('i', 'F')<cr>
xnoremap il :<c-u>call <SID>NextTextObject('i', 'F')<cr>

function! s:NextTextObject(motion, dir)
  let c = nr2char(getchar())

  if c ==# "b"
      let c = "("
  elseif c ==# "B"
      let c = "{"
  elseif c ==# "r"
      let c = "["
  endif

  exe "normal! ".a:dir.c."v".a:motion.c
endfunction

" }}}
" custom mappings {{{

" stop command line from showing accidentaly
map q: :q

" dont loose selection on indenting
vnoremap > >gv
vnoremap < <gv

" dot works in visual selection mode!
vnoremap . :norm.<cr>

" move by screen lines using arrow
nnoremap <Up> gk
nnoremap <Down> gj
inoremap <expr> <Down> pumvisible() ? "<Down>" : "<C-O>gj"
inoremap <expr> <Up> pumvisible() ? "<Up>" : "<C-O>gk"

" select whole buffer
nnoremap <silent> <leader>va ggVG

" move between windows
nnoremap <C-j> <C-w>j
nnoremap <C-k> <C-w>k
nnoremap <C-l> <C-w>l
nnoremap <C-h> <C-w>h

" tabs mappings
nnoremap <leader>tn :tabnext<cr>
nnoremap <leader>tp :tabprev<cr>
nnoremap <leader>tc :tabclose<cr>
nnoremap <leader>tN :tabnew<cr>

" buffer mappings
nnoremap <leader>bn :bnext<cr>
nnoremap <leader>bp :brevious<cr>
nnoremap <leader>bd :bdelete<cr>
nnoremap <leader>bN :new<cr>

" D deletes to the end of the line
nnoremap D d$

" better line begin (H) and end (L)
noremap H ^
noremap L $
vnoremap L g_

" don't move on *
nnoremap <silent> * :let stay_star_view = winsaveview()<cr>*:call winrestview(stay_star_view)<cr>

" keep search matches in the middle of the window
nnoremap n nzzzv
nnoremap N Nzzzv

" same when jumping around
nnoremap g; g;zz
nnoremap g, g,zz
nnoremap <c-o> <c-o>zz

" disable EX mode
nnoremap Q <nop>

" kill whitespace
nnoremap <silent> <leader>k :KillWhitespace<cr>:nohlsearch<cr>

" clean current search
nnoremap <silent> <leader>c :nohlsearch<cr>

" toggle list chars
nnoremap <silent> <leader>l :set list!<cr>

" toggle numbers
nnoremap <silent> <leader>n :set number!<cr>

" switch cwd to the directory of the open buffer
nnoremap <silent> <leader>d :cd %:p:h<cr>

" }}}
